apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "org.dyndns.warenix.palpal"
        minSdkVersion 15
        targetSdkVersion 20
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


configurations {
    patch
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':facebook')
    compile 'com.android.support:support-v4:20.+'
    compile 'de.greenrobot:eventbus:2.2.1'
    compile 'com.google.code.gson:gson:2.2.+'
    compile 'org.scribe:scribe:1.3+'
    compile 'com.squareup.picasso:picasso:2.3.3'

    compile files('lib/okhttp-urlconnection-2.0.0.patched.jar')

// compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'javax.annotation:jsr250-api:1.0'

    patch 'com.squareup.okio:okio:1.0.0'
    patch 'com.googlecode.jarjar:jarjar:1.3'
    patch 'com.squareup.okhttp:okhttp:2.0.0'
    patch 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'

    compile 'com.squareup.picasso:picasso:2.3.3'
}

project.ext.set("shouldBuildPatch",
                { !new File('lib/okhttp-urlconnection-2.0.0.patched.jar').exists() })

task downloadPatchLibs(type: Copy) {
    into('lib')
    from(configurations.patch)
    exclude('jarjar*')
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}
downloadPatchLibs.doFirst {
    if (!shouldBuildPatch()) {
        throw new StopExecutionException()
    }
}

task applyPatch(dependsOn: 'downloadPatchLibs') << {
    if (shouldBuildPatch()) {
        project.ant {
            taskdef name: "jarjar", classname: "com.tonicsystems.jarjar.JarJarTask",
                    classpath: configurations.patch.asPath
            jarjar(jarfile: 'lib/okhttp-urlconnection-2.0.0.patched.jar',
                   filesetmanifest: "merge") {
                zipfileset(src: 'lib/okio-1.0.0.jar')
                zipfileset(src: 'lib/okhttp-2.0.0.jar')
                zipfileset(src: 'lib/okhttp-urlconnection-2.0.0.jar')
                rule pattern: "okio.**", result: "custom.okio.@1"
            }
        }
    }
}

task cleanupDownloadPatchLibs(type: Delete, dependsOn: 'applyPatch') {
    delete 'lib/okio-1.0.0.jar'
    delete 'lib/okhttp-2.0.0.jar'
    delete 'lib/okhttp-urlconnection-2.0.0.jar'
}

assemble.dependsOn(cleanupDownloadPatchLibs)

task cleanPatch(type: Delete) {
    delete 'lib'
}
clean.dependsOn(cleanPatch)
